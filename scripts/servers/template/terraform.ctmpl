{{ range .Servers }}

resource "datadog_synthetics_test" "{{ .Domain | TerraformID }}_uptime_monitoring" {
  type    = "api"
  subtype = "http"
  name    = "{{ .Domain }}"
  message = ""
  tags    = [
    "site:{{ .Domain }}",
    "service:{{ .Domain }}",
    "project:masto-guide",
    "env:production",
  ]
  status = "live"

  # hit the /about page for the server
  request_definition {
    method = "GET"
    url    = "https://{{ .Domain }}/about"
  }

  # make it clear who is doing the monitoring
  request_headers = {
    X-About = "Request made as part of MastuGuide (https://guide.toot.as/dk/servers/)"
  }

  # site must respond within 500ms
  assertion {
    type     = "responseTime"
    operator = "lessThan"
    target   = 500
  }

  # and it must response with 2xx
  assertion {
    type     = "statusCode"
    operator = "matches"
    target   = "2\\d\\d" # any 2xx is OK
  }

  locations = [
    "aws:eu-south-1",
    "aws:eu-north-1",
    "aws:eu-west-1",
    "aws:eu-west-2",
    "aws:eu-west-3",
  ]

  options_list {
    # check every 60s
    tick_every = 60

    # Accept both HTTP1 and HTTP2 (but prefer HTTP2)
    http_version = "any"

    # TLS is important, and has to be well-configured
    accept_self_signed = false
    check_certificate_revocation = true
    ignore_server_certificate_error = false

    # Don't allow HTTP content when on HTTPS
    allow_insecure = false

    # must fail for 1 minute
    min_failure_duration = 60

    # and 3 servers has to agree
    min_location_failed = 3

    # allow a 300ms retry policy in case of maintenance
    retry {
      count    = 1
      interval = 300
    }

    monitor_options {
      renotify_interval = 0
    }
  }
}

resource "datadog_service_level_objective" "{{ .Domain | TerraformID }}_slo" {
  name        = "{{ .Domain }}"
  type        = "monitor"
  description = "Managed via TF"
  monitor_ids = [
    resource.datadog_synthetics_test.{{ .Domain | TerraformID }}_uptime_monitoring.monitor_id,
  ]

  thresholds {
    timeframe = "7d"
    target    = 99.9
  }

  thresholds {
    timeframe = "30d"
    target    = 99.9
  }

  thresholds {
    timeframe = "90d"
    target    = 99.9
  }

  tags = [
    "site:{{ .Domain }}",
    "service:{{ .Domain }}",
    "project:masto-guide",
    "env:production",
  ]
}

{{ end }}

// -------------------
// DASHBOARD
// -------------------

resource "datadog_dashboard" "overview" {
  title        = "Mastodon DK server monitoring"
  description  = "Created using the Datadog provider in Terraform"
  layout_type  = "ordered"
  reflow_type  = "fixed"

  lifecycle {
    create_before_destroy = true
  }

  template_variable {
    name             = "site"
    prefix           = "site"
    defaults         = ["*"]
    available_values = [
{{ range .Servers }}
      "{{ .Domain  }}",
{{- end }}
    ]
  }

  widget {
    note_definition {
      background_color = "green"
      content          = <<-EOT
            Hello! Welcome to the Danish Mastodon community monitoring service. All servers listed on the [**official Danish Mastodon Servers list**](https://guide.toot.as/dk/servers/) are automatically included in the monitoring you see below.

            Each server is monitored in the same way:

            * The server has to respond with `200 OK`
            * The server has to respond with `500 ms`

            Each server is checked once per minute, and in case of failure, an automatic retry is made after `300 ms`. If a server fail its checks for `1 minute` and `3 out of 5` locations agree, the server is registered as being down.
        EOT
      font_size        = "14"
      has_padding      = true
      show_tick        = false
      text_align       = "left"
      tick_edge        = "left"
      tick_pos         = "50%"
      vertical_align   = "top"
    }

    widget_layout {
      height          = 2
      is_column_break = false
      width           = 12
      x               = 0
      y               = 0
    }
  }

  widget {
    group_definition {
      title            = "Danish Mastodon Server monitoring"
      background_color = "vivid_blue"
      layout_type      = "ordered"
      show_title       = true

      widget {
        note_definition {
          background_color = "yellow"
          content          = <<-EOT
                **Monitoring status**

                Show each Mastodon server with their current check status and when they last triggered
            EOT
          font_size        = "14"
          has_padding      = true
          show_tick        = true
          text_align       = "center"
          tick_edge        = "bottom"
          tick_pos         = "50%"
          vertical_align   = "center"
        }

        widget_layout {
          height          = 1
          width           = 12
          y               = 1
          x               = 0
        }
      }

      widget {
        manage_status_definition {
          title               = "Monitoring overview"
          title_align         = "left"
          title_size          = 16
          color_preference    = "background"
          display_format      = "countsAndList"
          hide_zero_counts    = false
          query               = "tag:(project:masto-guide AND site:$site.value)"
          show_last_triggered = true
          sort                = "triggered,desc"
          summary_type        = "combined"
        }

        widget_layout {
          height          = 6
          is_column_break = false
          width           = 12
          x               = 0
          y               = 7
        }
      }

      widget {
        note_definition {
          background_color = "yellow"
          content          = <<-EOT
                **Monitoring log**

                Shows the individual outage and recovery events that has happened
            EOT
          font_size        = "14"
          has_padding      = true
          show_tick        = true
          text_align       = "center"
          tick_edge        = "bottom"
          tick_pos         = "50%"
          vertical_align   = "center"
        }

        widget_layout {
          height          = 1
          width           = 12
          y               = 7
          x               = 0
        }
      }

      widget {
        event_stream_definition {
          event_size  = "s"
          query       = "project:masto-guide $site @monitor.groups:\"*\"{{ ExcludedDomainsEventQuery .ExcludedDomains }}"
          title       = "Monitor log"
          title_align = "left"
          title_size  = "16"
        }

        widget_layout {
          height          = 6
          is_column_break = false
          width           = 12
          x               = 0
          y               = 8
        }
      }
    }

    widget_layout {
      height          = 15
      is_column_break = false
      width           = 12
      x               = 0
      y               = 2
    }
  }

  widget {
    group_definition {
      title            = "Performance monitoring"
      background_color = "vivid_pink"
      layout_type      = "ordered"
      show_title       = true

      widget {
        note_definition {
          background_color = "green"
          content          = <<-EOT
                Server response time is a crucial part of a great end-user experience.

                These graphs show the average response time for `2xx` responses (aka `200 OK`) in various ways to give you an sense of how fast of an experience you can expect from each.
            EOT
          font_size        = "16"
          has_padding      = false
          show_tick        = false
          text_align       = "center"
          tick_edge        = "left"
          tick_pos         = "50%"
          vertical_align   = "center"
        }


        widget_layout {
          height          = 1
          is_column_break = false
          width           = 12
          x               = 0
          y               = 0
        }
      }

      widget {
        timeseries_definition {
          title          = "75th percentile server response time for 2xx response codes (lower is better)"
          legend_layout  = "auto"
          show_legend    = true
          title_align    = "left"
          title_size     = "16"

          yaxis {
            include_zero = false
            max = "1000"
          }

          event {
            q = "project:masto-guide $site @monitor.groups:\"*\"{{ ExcludedDomainsEventQuery .ExcludedDomains }}"
          }

          legend_columns = [
            "avg",
            "max",
            "min",
            "sum",
            "value",
          ]

          marker {
            display_type = "ok bold"
            label        = "Fast"
            value        = "y < 250"
          }

          marker {
            display_type = "warning bold"
            label        = "Decent"
            value        = "250 < y < 500"
          }

          marker {
            display_type = "error bold"
            label        = "Slow"
            value        = "y > 500"
          }

          request {
            display_type   = "line"
            on_right_yaxis = false

            formula {
              formula_expression = "query1"
            }

            query {
              metric_query {
                data_source = "metrics"
                name        = "query1"
                query       = "p75:synthetics.http.response.time.distrib{project:masto-guide AND $site AND status_code_class:2xx{{ ExcludedDomainsQuery .ExcludedDomains }}} by {site}"
              }
            }

            style {
              line_type  = "solid"
              line_width = "normal"
              palette    = "semantic"
            }
          }
        }

        widget_layout {
          height          = 4
          is_column_break = false
          width           = 12
          x               = 0
          y               = 0
        }
      }

      widget {
        toplist_definition {
          title       = "7th percentile response time for 2xx response code (lower is better)"
          title_align = "left"
          title_size  = "16"

          request {
            conditional_formats {
              comparator = "<="
              hide_value = false
              palette    = "white_on_green"
              value      = 250
            }

            conditional_formats {
              comparator = "<="
              hide_value = false
              palette    = "white_on_yellow"
              value      = 500
            }

            conditional_formats {
              comparator = ">"
              hide_value = false
              palette    = "white_on_red"
              value      = 500
            }

            formula {
              formula_expression = "query2"

              limit {
                count = 500
                order = "asc"
              }
            }

            query {
              metric_query {
                aggregator  = "percentile"
                data_source = "metrics"
                name        = "query2"
                query       = "p75:synthetics.http.response.time.distrib{project:masto-guide AND $site AND status_code_class:2xx{{ ExcludedDomainsQuery .ExcludedDomains }}} by {site}.fill(zero)"
              }
            }
          }
        }

        widget_layout {
          height          = 3
          is_column_break = false
          width           = 6
          x               = 0
          y               = 4
        }
      }

      widget {
        change_definition {
          title       = "75th percentile response time change since the day before for 2xx response codes (lower is better)"
          title_align = "left"
          title_size  = "16"

          request {
            change_type   = "absolute"
            increase_good = false
            order_by      = "present"
            order_dir     = "asc"
            show_present  = false

            formula {
              formula_expression = "day_before(query1)"
            }

            formula {
              formula_expression = "query1"
            }

            query {
              metric_query {
                aggregator  = "percentile"
                data_source = "metrics"
                name        = "query1"
                query       = "p75:synthetics.http.response.time.distrib{project:masto-guide AND $site AND status_code_class:2xx{{ ExcludedDomainsQuery .ExcludedDomains }}} by {site}"
              }
            }
          }
        }

        widget_layout {
          height          = 3
          is_column_break = false
          width           = 6
          x               = 6
          y               = 4
        }
      }
    }

    widget_layout {
      height          = 8
      is_column_break = false
      width           = 12
      x               = 0
      y               = 8
    }
  }

  widget {
    group_definition {
      title            = "Individual SLO status"
      background_color = "vivid_purple"
      layout_type      = "ordered"
      show_title       = true

      widget {
        note_definition {
          background_color = "green"
          font_size        = "14"
          has_padding      = false
          show_tick        = false
          text_align       = "center"
          tick_edge        = "left"
          tick_pos         = "50%"
          vertical_align   = "center"
          content          = <<-EOT
                The target [SLO](https://docs.datadoghq.com/monitors/service_level_objectives/) is **[99.9%](https://uptime.is/99.9)** Meaning downtime in total per

                **Day:** 1m 26s | **Week:** 10m 4.8s | **Month:** 43m 28s | **Quarter:** 2h 10m 24s | **Year:** 8h 41m 38s
            EOT
        }

        widget_layout {
          height          = 1
          is_column_break = false
          width           = 12
          x               = 0
          y               = 0
        }
      }

{{ range $index, $elem := .Servers }}
      widget {
        service_level_objective_definition {
          show_error_budget = true
          slo_id            = resource.datadog_service_level_objective.{{ TerraformID $elem.Domain }}_slo.id

          time_windows      = [
              "7d",
              "30d",
              "90d",
            ]
          title             = "SLO status for: {{ $elem.Domain }}"
          title_align       = "left"
          title_size        = "16"
          view_mode         = "overall"
          view_type         = "detail"
        }

        widget_layout {
          height          = 4
          width           = 6
          x               = {{ DD_SplitIntoFourX $index }}
          y               = {{ DD_SplitIntoFourY $index }}
          is_column_break = false
        }
      }
{{ end }}
    }

    widget_layout {
      height          = 22
      is_column_break = false
      width           = 12
      x               = 0
      y               = 15
    }
  }
}
